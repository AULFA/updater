dependencies {
  implementation project(":au.org.libraryforall.updater.apkinstaller.api")
  implementation project(":au.org.libraryforall.updater.apkinstaller.device")
  implementation project(":au.org.libraryforall.updater.installed.api")
  implementation project(":au.org.libraryforall.updater.installed.vanilla")
  implementation project(":au.org.libraryforall.updater.inventory.api")
  implementation project(":au.org.libraryforall.updater.inventory.vanilla")
  implementation project(":au.org.libraryforall.updater.repository.api")
  implementation project(":au.org.libraryforall.updater.repository.xml.api")
  implementation project(":au.org.libraryforall.updater.repository.xml.spi")
  implementation project(":au.org.libraryforall.updater.repository.xml.v1_0")
  implementation project(":au.org.libraryforall.updater.repository.xml.v2_0")

  implementation libraries.androidMaterial
  implementation libraries.androidXActivity
  implementation libraries.androidXAppCompat
  implementation libraries.androidXConstraintLayout
  implementation libraries.androidXCore
  implementation libraries.androidXWorkManager
  implementation libraries.conductor
  implementation libraries.irradiaHTTPAPI
  implementation libraries.irradiaHTTPVanilla
  implementation libraries.kotlinStdlib
  implementation libraries.logbackAndroid
  implementation libraries.rxjava

  implementation (libraries.slf4j) {
    force = true
  }
}

def versionValues() {
  def propsFile = file('version.properties')
  def Properties props = new Properties()
  def code
  if (propsFile.canRead()) {
    props.load(new FileInputStream(propsFile))
    code = props['versionCode'].toInteger()
  } else {
    throw new FileNotFoundException("Could not read version.properties!")
  }

  props['versionCode'] = (code + 1).toString()
  props.store(new FileOutputStream(propsFile), "")

  logger.info("incrementing build version ${code} -> ${code + 1}")
  return code
}

def getGitHash = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

android {
  def version = versionValues()

  signingConfigs {
    debug {
      keyAlias findProperty("au.org.libraryforall.keyAlias")
      keyPassword findProperty("au.org.libraryforall.keyPassword")
      storeFile file("${project.rootDir}/lfa-keystore.jks")
      storePassword findProperty("au.org.libraryforall.storePassword")
    }
    release {
      keyAlias findProperty("au.org.libraryforall.keyAlias")
      keyPassword findProperty("au.org.libraryforall.keyPassword")
      storeFile file("${project.rootDir}/lfa-keystore.jks")
      storePassword findProperty("au.org.libraryforall.storePassword")
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
    release {
      signingConfig signingConfigs.release
    }
  }

  defaultConfig {
    buildConfigField "String", "GIT_COMMIT", "\"${getGitHash()}\""
    versionName = VERSION_NAME
    versionCode = version
    setProperty("archivesBaseName", "lfa-updater-${versionName}-${versionCode}")
  }
}
