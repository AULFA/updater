import java.security.MessageDigest

buildscript {
  if ("$gradle.gradleVersion" != "6.7") {
    throw new GradleException("Gradle version 6.7 is required (received $gradle.gradleVersion)")
  }

  ext.kotlin_version = "1.4.20"

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.2.0"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'com.android.tools.build:gradle:4.1.2'
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.36.0"
  id "digital.wup.android-maven-publish" version "3.6.3"
  id "ru.vyarus.animalsniffer" version "1.5.2"
}

ext {
  androidBuildToolsVersion = "29.0.2"
  androidCompileSDKVersion = 28
  androidMinimumSDKVersion = 21
  androidTargetSDKVersion = 28

  if (project.hasProperty("one.lfa.directory.publish")) {
    useDirectoryPublishPath = project.property("one.lfa.directory.publish")
    useDirectoryPublish = true
    logger.warn("one.lfa.directory.publish is set: We will publish artifacts to ${useDirectoryPublishPath}")
  } else {
    logger.warn("one.lfa.directory.publish is not set: We will not publish artifacts to a custom directory")
    useDirectoryPublish = false
    useDirectoryPublishPath = null
  }
}

ext.versions = [
  android_material              : '1.2.1',
  androidx_activity             : '1.1.0',
  androidx_appcompat            : '1.2.0',
  androidx_constraintlayout     : '1.1.3',
  androidx_core_ktx             : '1.3.2',
  androidx_lifecycle            : '2.2.0',
  androidx_lifecycle_viewmodel  : '2.2.0',
  androidx_recyclerview         : '1.1.0',
  androidx_work_manager         : '2.2.0',
  conductor                     : '3.0.0',
  google_guava                  : '27.1-android',
  irradia_http                  : '0.0.3',
  joda_time                     : '2.10.9',
  junit                         : '4.13.1',
  osgi_bundle_annotation        : '1.1.0',
  osgi_bundle_version_annotation: '1.1.1',
  rxjava2                       : '2.2.20',
]

ext.libraries = [
  android_material              : "com.google.android.material:material:${versions.android_material}",
  androidx_activity             : "androidx.activity:activity-ktx:${versions.androidx_activity}",
  androidx_appcompat            : "androidx.appcompat:appcompat:${versions.androidx_appcompat}",
  androidx_constraintlayout     : "androidx.constraintlayout:constraintlayout:${versions.androidx_constraintlayout}",
  androidx_core                 : "androidx.core:core-ktx:${versions.androidx_core_ktx}",
  androidx_recyclerview         : "androidx.recyclerview:recyclerview:${versions.androidx_recyclerview}",
  androidx_work_manager         : "androidx.work:work-runtime-ktx:${versions.androidx_work_manager}",
  conductor                     : "com.bluelinelabs:conductor:${versions.conductor}",
  google_guava                  : "com.google.guava:guava:${versions.google_guava}",
  irradia_http_api              : "one.irradia.http:one.irradia.http.api:${versions.irradia_http}",
  irradia_http_vanilla          : "one.irradia.http:one.irradia.http.vanilla:${versions.irradia_http}",
  jackson                       : "com.fasterxml.jackson.core:jackson-core:2.9.8",
  jcip                          : "com.io7m.jcip:jcip-annotations:1.0.2",
  joda_time                     : "joda-time:joda-time:${versions.joda_time}",
  junit                         : "junit:junit:${versions.junit}",
  kotlin_reflect                : "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
  kotlin_stdlib                 : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
  logback_android               : "com.github.tony19:logback-android:2.0.0",
  logback_classic               : "ch.qos.logback:logback-classic:1.3.0-alpha4",
  mockito                       : "org.mockito:mockito-core:2.27.0",
  nanohttpd                     : 'org.nanohttpd:nanohttpd:2.3.1',
  okhttp                        : "com.squareup.okhttp3:okhttp:3.14.1",
  osgi_bundle_annotation        : "org.osgi:org.osgi.annotation.bundle:${versions.osgi_bundle_annotation}",
  osgi_bundle_version_annotation: "org.osgi:org.osgi.annotation.versioning:${versions.osgi_bundle_version_annotation}",
  rxjava                        : "io.reactivex.rxjava2:rxjava:${versions.rxjava2}",
  slf4j                         : "org.slf4j:slf4j-api:1.7.25",
  xerces                        : "xerces:xercesImpl:2.12.0"
]

allprojects {
  group = project.ext["GROUP"]
  version = project.ext["VERSION_NAME"]
}

static def sha256Of(File file) {
  MessageDigest digest = MessageDigest.getInstance("SHA-256")
  digest.update(file.getBytes())
  return digest.digest().encodeHex().toString()
}

task preFlightChecks {
  logger.info("running pre-flight checks")

  def keystoreHash = "df23e1d756802f420a8add6d6104848a4ae110d5de946267cccb9ea8b2e892dd"

  def requiredFiles = [:]
  requiredFiles["lfa-keystore.jks"] = keystoreHash
  requiredFiles["one.lfa.updater.app/src/main/assets/bundled_credentials.xml"] = "3186190d091144e2c108a3d0675d5176b6396310f0abdc9071c1c283ed431893"
  requiredFiles["one.lfa.updater.inclusiv/src/main/assets/bundled_credentials.xml"] = "3186190d091144e2c108a3d0675d5176b6396310f0abdc9071c1c283ed431893"

  def missingFiles = []
  for (entry in requiredFiles) {
    logger.info("Checking ${entry.key} exists and has expected digest value")

    try {
      def received = sha256Of(file(entry.key))
      def expected = entry.value
      if (received != expected) {
        logger.error("ERROR: File ${entry.key} has incorrect SHA-256 hash")
        logger.error("  Expected: ${expected}")
        logger.error("  Received: ${received}")
        missingFiles.add(entry.key)
      }
    } catch (Exception e) {
      logger.error("ERROR: Could not open required file ${entry.key}: ${e.getClass()}: ${e.getMessage()}")
      missingFiles.add(entry.key)
    }
  }

  if (!missingFiles.isEmpty()) {
    throw new FileNotFoundException("One or more required files are missing or have incorrect contents.")
  }
}

subprojects { project ->

  switch (POM_PACKAGING) {
    case "jar":
      logger.info("Configuring ${project} (${POM_PACKAGING}) as jar project")

      apply plugin: "java"
      apply plugin: "java-library"
      apply plugin: "kotlin"

      java {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
      }

      compileKotlin {
        kotlinOptions {
          jvmTarget = "1.8"
        }
      }

      /*
       * Apply the Animal Sniffer plugin to check that code is Android compatible.
       */

      apply plugin: "ru.vyarus.animalsniffer"

      dependencies {
        signature "org.codehaus.mojo.signature:java16:1.1@signature"
        signature "net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature"
      }

      /*
       * Build an OSGi bundle.
       */

      apply plugin: "biz.aQute.bnd.builder"

      jar {
        bnd """
Automatic-Module-Name: ${POM_AUTOMATIC_MODULE_NAME}

-removeheaders: Bnd-LastModified, Tool, Private-Package
"""
      }
      break

    case "apk":
      logger.info("Configuring ${project} (${POM_PACKAGING}) as Android application project")

      apply plugin: "com.android.application"
      apply plugin: "kotlin-android"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
        }
        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
          jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
      }
      break

    case "aar":
      logger.info("Configuring ${project} (${POM_PACKAGING}) as Android library project")

      apply plugin: "com.android.library"
      apply plugin: "kotlin-android"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
          testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
          jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
        testOptions {
          execution "ANDROID_TEST_ORCHESTRATOR"
          animationsDisabled = true
        }
      }
      break

    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  task javadocsJar(type: Jar) {
    classifier = "javadoc"
  }

  task sourcesJar(type: Jar) {
    classifier = "sources"
    from "src/main/java", "src/main/resources"
  }

  repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
  }
}

/**
 * Empty tasks called by CI. We don't do semantic versioning analysis or linting here yet.
 */

task verifySemanticVersioning {

}

task ktlint {

}
